# VPC Configuration
resource "aws_vpc" "main" {
  cidr_block           = var.vpc_cidr
  enable_dns_support   = true
  enable_dns_hostnames = true

  tags = {
    Name = "${var.team_name}-vpc"
    Owner = var.asset_owner_name
    Environment = "${var.team_name}-${var.environment}"
  }

  lifecycle {
    ignore_changes = [ tags ]
  }
}

# DHCP Options Set with custom + AWS DNS
resource "aws_vpc_dhcp_options" "custom_dns" {
  domain_name         = var.domain_name
  domain_name_servers = [var.dns_server_ip, "AmazonProvidedDNS"]

  tags = {
    Name  = "${var.team_name}-dhcp-options"
    Owner = var.asset_owner_name
    Environment = "${var.team_name}-${var.environment}"
  }
}

# Associate DHCP Options with VPC
resource "aws_vpc_dhcp_options_association" "main" {
  vpc_id          = aws_vpc.main.id
  dhcp_options_id = aws_vpc_dhcp_options.custom_dns.id
}

# Internet Gateway
resource "aws_internet_gateway" "igw" {
  vpc_id = aws_vpc.main.id

  tags = {
    Name = "${var.team_name}-igw"
    Owner = var.asset_owner_name
    Environment = "${var.team_name}-${var.environment}"
  }
}

#Public Subnet
resource "aws_subnet" "public" {
  vpc_id                  = aws_vpc.main.id
  cidr_block              = var.public_subnet_cidr
  availability_zone       = var.public_subnet_az
  map_public_ip_on_launch = true

  tags = {
    Name = "${var.team_name}-${var.environment}-public-subnet"
    Owner = var.asset_owner_name
    Environment = "${var.team_name}-${var.environment}"
  }

  lifecycle {
    ignore_changes = [ tags ]
  }
}

# Private Subnet
resource "aws_subnet" "private" {
  vpc_id                  = aws_vpc.main.id
  cidr_block              = var.private_subnet_cidr
  availability_zone       = var.private_subnet_az
  map_public_ip_on_launch = false

  tags = {
    Name = "${var.team_name}-${var.environment}-private-subnet"
    Owner = var.asset_owner_name
    Environment = "${var.team_name}-${var.environment}"
  }

  lifecycle {
    ignore_changes = [ tags ]
  }
}

# Elastic IP for NAT Gateway
resource "aws_eip" "nat_eip" {
  tags = {
    Name = "${var.team_name}-${var.environment}-nat-eip"
    Owner = var.asset_owner_name
    Environment = "${var.team_name}-${var.environment}"
  }
  lifecycle {
    ignore_changes = [ tags ]
  }
}

# NAT Gateway in public subnet
resource "aws_nat_gateway" "nat" {
  allocation_id = aws_eip.nat_eip.id
  subnet_id     = aws_subnet.public.id

  tags = {
    Name = "${var.team_name}-${var.environment}-nat-gateway"
    Owner = var.asset_owner_name
    Environment = "${var.team_name}-${var.environment}"
  }

  depends_on = [aws_internet_gateway.igw]
}

# Route Table for Public Subnet
resource "aws_route_table" "public" {
  vpc_id = aws_vpc.main.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.igw.id
  }

  tags = {
    Name = "${var.team_name}-${var.environment}-public-rt"
    Owner = var.asset_owner_name
    Environment = "${var.team_name}-${var.environment}"
  }
}

# Associate Public Subnet with Route Table
resource "aws_route_table_association" "public" {
  subnet_id      = aws_subnet.public.id
  route_table_id = aws_route_table.public.id
}

# Route Table for Private Subnet
resource "aws_route_table" "private" {
  vpc_id = aws_vpc.main.id

  route {
    cidr_block     = "0.0.0.0/0"
    nat_gateway_id = aws_nat_gateway.nat.id
  }

  tags = {
    Name = "${var.team_name}-${var.environment}-private-rt"
    Owner = var.asset_owner_name
    Environment = "${var.team_name}-${var.environment}"
  }
}

# Associate Private Subnet with Private Route Table
resource "aws_route_table_association" "private" {
  subnet_id      = aws_subnet.private.id
  route_table_id = aws_route_table.private.id
}


resource "aws_vpc_endpoint" "s3" {
  vpc_id            = aws_vpc.main.id
  service_name      = "com.amazonaws.${var.region}.s3"
  vpc_endpoint_type = "Gateway"
  route_table_ids   = [aws_route_table.private.id]
  tags = { 
    Name = "${var.team_name}-${var.environment}-s3-gateway-endpoint" 
    Owner = var.asset_owner_name
    Environment = "${var.team_name}-${var.environment}"
  }
}
